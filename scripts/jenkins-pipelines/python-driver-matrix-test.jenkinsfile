#!groovy
// Pipeline python-driver-matrix-test
// In order to debug, set RELENG_BRANCH on Jenkins job with value of the debug branch.
// example: refs/heads/build-pipeline-into-scm

Map modules = [:]
pipeline {
	parameters {
		//Mandatory:
		string(name: 'X86_NODE_PARAM', defaultValue: 'packager', description: 'On which node to run. Useful options: gce-qavpc,gce2-qavpc,gce4-qavpc. packager machines are slower, but could be used:loki,dgccd201,thor,ran,sif,huginn,muninn')
		//Have defaults:
		string(name: 'VERSIONS', defaultValue: '3.21.0,3.22.0', description: 'versions to check')
		string(name: 'PROTOCOLS', defaultValue: '3,4', description: 'CQL binary protocols v3, v4. i.e. 3,4')
		string(name: 'ARTIFACT_SOURCE_JOB_NAME', defaultValue: '', description: 'Build path to take artifacts from (usually next or build). Use this with ARTIFACT_SOURCE_BUILD_NUM to get reloc from jenkins, or ARTIFACT_WEB_URL to take them from cloud.')
		string(name: 'ARTIFACT_SOURCE_BUILD_NUM', defaultValue: '', description: 'Build number to take artifacts from. Leave empty to use last available build. Use with ARTIFACT_SOURCE_JOB_NAME to take artifacts from Jenkins, or ARTIFACT_WEB_URL to take them from cloud.')
		string(name: 'ARTIFACT_WEB_URL', defaultValue: 'latest', description: 'URL to take reloc items from. Use when reloc is not available on jenkins, or when running on AWS, which will download faster from S3.')
		string(name: 'SCYLLA_CCM_BRANCH', defaultValue: '', description: 'git branch or sha for CCM. i.e. branch-3.1, master or a sha')
		string(name: 'SCT_BRANCH', defaultValue: '', description: 'git branch or sha for SCT. i.e. branch-3.1. master or a sha')
		// The adding_docker_image is temp, till Israel's PR is approved.
		string(name: 'PYTHON_DRIVER_MATRIX_BRANCH', defaultValue: '', description: 'git branch or sha for Python driver matrix. i.e. branch-3.1. master or a sha')
    string(name: 'RELENG_REPO', defaultValue: '', description: 'Branch of pkg repo or fork. If empty - will take scylla-pkg repo (examples: git@github.com:scylladb/scylla-pkg, git@github.com:scylladb/scylla-enterprise-pkg, git@github.com:hagitsegev/scylla-pkg)')
		string(name: 'RELENG_BRANCH', defaultValue: '', description: 'Branch of pkg repo. If empty - will take master branch (master, branch-3.1)')
		booleanParam(name: 'DRY_RUN', defaultValue: false, description: 'Set to test the pipeline code')
		booleanParam(name: 'PRESERVE_WORKSPACE', defaultValue: false, description: 'Set to avoid cleaning workspace before and after run, for debug needs. If you do so - pay attention to the X86_NODE_PARAM.')
		booleanParam(name: 'DEBUG_MAIL', defaultValue: false, description: 'Set to send mails to requester only')
	}
	agent {
		label {
			label env.X86_NODE_PARAM
		}
	}

	environment {
		AWS_ACCESS_KEY_ID     = credentials('jenkins2-aws-secret-key-id')
		AWS_SECRET_ACCESS_KEY = credentials('jenkins2-aws-secret-access-key')
	}

	options {
		disableConcurrentBuilds()
		timeout(time: 7, unit: 'HOURS')
		buildDiscarder(
			logRotator(
				numToKeepStr: '10',
				daysToKeepStr: env.JOB_NAME.contains("releng-testing") ? '14': ''
			)
		)
	}

	stages {

		stage ('Prepare') {
			steps {
				script {
					generalProperties = readProperties file: 'scripts/jenkins-pipelines/general.properties'
					general = load "${generalProperties.groovyPath}/general.groovy"
					general.initPipeline()

					artifactSourceJob = params.ARTIFACT_SOURCE_JOB_NAME ?: "${branchProperties.calledBuildsDir}${branchProperties.relocUploaderJobName}"
					pythonDriverScyllaTestDone = false
					pythonDriverDatastaxTestDone = false
					buildMode = "release"
					pythonDriverDatastaxCheckoutDir = "python-driver-datastax"
					pythonDriverScyllaCheckoutDir = "python-driver-scylla"
					artifactSourceJobNum = params.ARTIFACT_SOURCE_BUILD_NUM ?: Jenkins.instance.getItemByFullName(artifactSourceJob).lastSuccessfulBuild.number.toString()
					paramArtifactWebUrl = params.ARTIFACT_WEB_URL ?: "latest"
					cloudUrl = artifact.getLastSuccessfulUrl (
						artifactWebUrl: paramArtifactWebUrl,
						artifactSourceJob: artifactSourceJob,
						artifactSourceJobNum: artifactSourceJobNum,
						fieldName: "reloc-pack-url:"
					)
				}
			}
		}

		stage('Checkout') {
			steps {
				script {
					ccmBranch	= params.SCYLLA_CCM_BRANCH ?: branchProperties.stableBranchName
					sctBranch	= params.SCT_BRANCH ?: branchProperties.stableQABranchName
					pythonDriverMatrixBranch = params.PYTHON_DRIVER_MATRIX_BRANCH ?: branchProperties.stableDriverMatrixBranchName

					git.cleanWorkSpaceUponRequest(params.PRESERVE_WORKSPACE)
					git.checkoutToDir (relengRepo, relengBranch, gitProperties.scyllaPkgCheckoutDir, disableSubmodules)
					git.checkoutToDir (generalProperties.pythonDriverMatrixRepoUrl, pythonDriverMatrixBranch, generalProperties.pythonDriverMatrixCheckoutDir)
					git.checkoutToDir (gitProperties.scyllaCcmRepoUrl, ccmBranch, gitProperties.scyllaCcmCheckoutDir)
					git.checkoutToDir (generalProperties.scyllaClusterTestsRepoUrl, sctBranch, generalProperties.scyllaClusterTestsCheckoutDir)
					git.checkoutToDir ("https://github.com/datastax/python-driver.git", "master", pythonDriverDatastaxCheckoutDir)
					git.checkoutToDir ("https://github.com/scylladb/python-driver.git", "master", pythonDriverScyllaCheckoutDir)
					general.lsPath(WORKSPACE)
				}
			}
		}

		stage('Copy Artifacts') {
			steps {
				script {
					artifact.getRelocArtifacts (
						buildMode: buildMode,
						cloudUrl: cloudUrl,
						architecture: branchProperties.x86ArchName,
					)
				}
			}
		}

		stage('Python Driver Matrix Test(datastax)') {
			steps {
				catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
					script {
						test.doPythonDriverMatrixTest(
							dryRun: params.DRY_RUN,
							pythonDriverScyllaOrDatastaxCheckoutDir: "$WORKSPACE/$pythonDriverDatastaxCheckoutDir",
							pythonDriverMatrixCheckoutDir: "$WORKSPACE/$generalProperties.pythonDriverMatrixCheckoutDir",
							driverType: "datastax",
							pythonDriverVersions: params.VERSIONS,
							cqlBinaryProtocols: params.PROTOCOLS,
							architecture: branchProperties.x86ArchName,
						)
						pythonDriverDatastaxTestDone = true
					}
				}
			}
		}
		stage('Python Driver Matrix Test(scylla)') {
			steps {
				catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
					script {
						test.doPythonDriverMatrixTest(
							dryRun: params.DRY_RUN,
							pythonDriverScyllaOrDatastaxCheckoutDir: "$WORKSPACE/$pythonDriverScyllaCheckoutDir",
							pythonDriverMatrixCheckoutDir: "$WORKSPACE/$generalProperties.pythonDriverMatrixCheckoutDir",
							driverType: "scylla",
							pythonDriverVersions: params.VERSIONS,
							cqlBinaryProtocols: params.PROTOCOLS,
							architecture: branchProperties.x86ArchName,
						)
						pythonDriverScyllaTestDone = true
					}
				}
			}
		}
	}

	post {
		//Order is: always, changed, fixed, regression, aborted, failure, success, unstable, and cleanup.
		always {
			script {
				if (binding.hasVariable('initPipelineDone')) {
					if (pythonDriverDatastaxTestDone && pythonDriverScyllaTestDone && ! params.DRY_RUN) {
						publishStatus = artifact.publishArtifactsStatus("**/*.xml", "$WORKSPACE/${generalProperties.pythonDriverMatrixCheckoutDir}")
						boolean needToPublish = true
						publishStatus |= test.publishTestResults(needToPublish, "**/*.xml", "junit", "$WORKSPACE/${generalProperties.pythonDriverMatrixCheckoutDir}")
						if (publishStatus) {
							echo "Error: Could not publish some item(s)."
							logText += "\nCould not publish some item(s). See log for details"
							if (currentBuild.currentResult != "ABORTED") {
								currentBuild.result = 'FAILURE'
							}
						}
					}
					mail.mailIfError("$devAddress", "${currentBuild.currentResult}: $jobTitle", "${currentBuild.currentResult}: ${jobTitle}: $logText")
					git.cleanWorkSpaceUponRequest(params.PRESERVE_WORKSPACE)
				} else if (!(JOB_NAME.contains("releng-testing") || params.DRY_RUN || params.DEBUG_MAIL)) {
					mail (
						to: "releng@scylladb.com",
						subject: "${currentBuild.currentResult}: ${env.JOB_NAME} [${env.BUILD_NUMBER}]",
						body: "${env.JOB_NAME} [${env.BUILD_NUMBER}] ${currentBuild.currentResult} at the very beginning. Please see Jenkins logs for details"
					)
					cleanWs()
				} else {
					cleanWs()
				}
			}
		}

		fixed {
			script {
				mail.mailFixed("$devAddress", "Fixed ${currentBuild.currentResult}: $jobTitle", "Fixed ${currentBuild.currentResult}: ${jobTitle}: $logText")
			}
		}
	}
}
